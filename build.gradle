apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'announce'

springVersion = '3.1.0.M2'

repositories {

	mavenLocal()
	mavenRepo urls: 'http://maven.springframework.org/milestone'
	mavenRepo urls: 'http://maven.springframework.org/release'
	mavenRepo urls: 'https://github.com/s7/mvnrepo/raw/master'
	mavenRepo urls: 'https://repository.jboss.org/nexus/content/groups/developer/'
	mavenRepo urls: 'https://repository.apache.org/content/repositories/snapshots'
	mavenCentral()

}

configurations {

	compileOnly

	provided

	testCompile.extendsFrom provided
	compile.transitive = false
	testCompile.transitive = false

}

dependencies {

	// Servlet API will be provided by the servlet container
	compileOnly "javax.servlet:servlet-api:2.5"

	// Spring Framework
	compile group: 'org.springframework', name: 'spring-core', version: "$springVersion"
	compile group: 'org.springframework', name: 'spring-beans', version: "$springVersion"
	compile group: 'org.springframework', name: 'spring-context', version: "$springVersion"
	compile group: 'org.springframework', name: 'spring-web', version: "$springVersion"
	runtime group: 'org.springframework', name: 'spring-webmvc', version: "$springVersion"

	// Spring Security
	compile group: 'org.springframework.security', name: 'spring-security-core', version: "3.1.0.RC2"
	runtime group: 'org.springframework.security', name: 'spring-security-web', version: "3.1.0.RC2"
	runtime group: 'org.springframework.security', name: 'spring-security-config', version: "3.1.0.RC2"

	// JSON support
	runtime group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.6.7'

	// JSR 303 (validation) annotations and implementation
	compile group: 'javax.validation', name: 'validation-api', version: '1.0.0.GA'
	runtime group: 'org.hibernate', name: 'hibernate-validator', version: '4.0.2.GA'

	// JSR 330 (DI) annotations
	compile group: 'javax.inject', name: 'javax.inject', version: '1'

	// persistence layer
	compile group: 'org.springframework', name: 'spring-tx', version: "$springVersion"
	compile group: 'org.scale7', name: 'scale7-pelops', version: '1.1-0.7.x'
	compile group: 'org.apache.cassandra', name: 'cassandra-all', version: '0.7.8'

	// utilities
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

	// logging via SLF4J -> LOG4J
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
	runtime group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.6.1'
	runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
	runtime group: 'log4j', name: 'log4j', version: '1.2.16'

	// testing and mocking
	testCompile group: 'junit', name: 'junit', version: '4.8.2'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
	testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3.RC2'

}

description = 'Robo Race'
group = 'net.skhome'
version = '1.0.0.SNAPSHOT'

[configurations.runtime, configurations.default]*.exclude(module: "commons-logging")
[configurations.runtime, configurations.default]*.exclude(module: "guice")

sourceSets.main.compileClasspath += configurations.compileOnly
sourceSets.test.compileClasspath += configurations.compileOnly
sourceSets.main.compileClasspath += configurations.provided
sourceSets.test.compileClasspath += configurations.provided

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

