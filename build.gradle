apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'

springVersion = '3.1.0.M2'

repositories {
	mavenLocal()
	mavenRepo urls: 'http://maven.springframework.org/milestone'
	mavenRepo urls: 'http://maven.springframework.org/release'
	mavenRepo urls: 'https://github.com/s7/mvnrepo/raw/master'
	mavenRepo urls: 'https://repository.jboss.org/nexus/content/groups/developer/'
	mavenCentral()
}

configurations {
    
    compileOnly
    
    provided
    
    testCompile.extendsFrom provided
    compile.transitive = false
    testCompile.transitive = false
    
}

dependencies {
	
	// Servlet API will be provided by the servlet container
	provided "javax.servlet:servlet-api:2.5"
	
	// Spring Framework
	compile group: 'org.springframework', name: 'spring-core',    version: "$springVersion"
	compile group: 'org.springframework', name: 'spring-beans',   version: "$springVersion"
	compile group: 'org.springframework', name: 'spring-context', version: "$springVersion"
	compile group: 'org.springframework', name: 'spring-web',     version: "$springVersion"
	runtime group: 'org.springframework', name: 'spring-webmvc',  version: "$springVersion"
	
	// Spring Security
	compile group: 'org.springframework.security', name: 'spring-security-core',   version: "$springVersion"
	runtime group: 'org.springframework.security', name: 'spring-security-web',    version: "$springVersion"
	runtime group: 'org.springframework.security', name: 'spring-security-config', version: "$springVersion"
	
	// JSON support
	runtime group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.6.+'
	
	// JSR 303 (validation) annotations and implementation
	compile group: 'javax.validation', name: 'validation-api', version: '1.0.0.GA'
	runtime group: 'org.hibernate', name: 'hibernate-validator', version: '4.0.2.GA'
	
	// JSR 330 (DI) annotations
	compile group: 'javax.inject', name:'javax.inject', version: '1'
	
	// persistence layer
	compile group: 'org.scale7', name:'scale7-pelops', version: '1.1-0.7.x'
	compile group: 'org.apache.cassandra', name:'cassandra-all', version: '0.7.+'
	
	// logging via SLF4J -> LOG4J
	compile group: 'org.slf4j', name:'slf4j-api', version: '1.6.+'
	runtime group: 'org.slf4j', name:'jcl-over-slf4j', version: '1.6.+'
	runtime group: 'org.slf4j', name:'slf4j-log4j12', version: '1.6.+'
	runtime group: 'log4j', name:'log4j', version: '1.2.+'
	
	// testing and mocking
	testCompile group: 'junit', name: 'junit', version: '4.8.+'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.+'
	testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3.+'
	
}

description = 'Robo Race'
group = 'net.skhome'
version = '1.0.0.SNAPSHOT'

[configurations.runtime, configurations.default]*.exclude(module: "commons-logging")
[configurations.runtime, configurations.default]*.exclude(module: "guice")

sourceSets.main.compileClasspath += configurations.compileOnly
sourceSets.main.compileClasspath += configurations.provided

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.6
targetCompatibility = 1.6

